
# ===================== #
# Faroese disambiguator #
# ===================== #


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<¶>";



# ============= #
# Tags and sets #
# ============= #


# ========
   SETS
# ========

LIST BOS = (>>>) (<s>); 
LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

LIST N = n np ;
LIST V = vblex vbser ;
LIST A = adj ;
LIST Prop = np ;
LIST Adv = adv ;
LIST Num = num ;
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST Interj = ij ;
LIST Abbr = Abbr ;#TODO
LIST ACR = acr ;
LIST Pr = pr ;
LIST Pron = prn ;
LIST Pers = p1 p2 p3 ;
LIST Det = det ;
LIST Dem = dem ;
LIST Refl = Refl ;#TODO
LIST Recipr = Recipr ;#TODO
LIST Poss = pos ;
LIST Interr = itg ;
LIST IM = IM ;#TODO

LIST Nom = nom ;
LIST Acc = acc ;
LIST Gen = gen ;
LIST Dat = dat ;
LIST Msc = m ;
LIST Fem = f ;
LIST Neu = nt ;
LIST Sg = sg ;
LIST Pl = pl ;
LIST Def = def ;
LIST Indef = ind ;
LIST Cmp = comp ;
LIST Superl = sup ;
LIST Prs = pres ;
LIST Prt = past ;
LIST 1Sg = p1 sg ;
LIST 2Sg = p2 sg ;
LIST 3Sg = p3 sg ;
LIST Inf = inf ;
LIST PrfPrc = pp ;#TODO is PrfPrc past participle?
LIST PrsPrc = ppres ;#TODO is PrsPrC present participle?
LIST Ind = pri ;
LIST Imp = imp ;
LIST Sbj = Sbj ;#TODO
LIST Sup = supn ;
LIST Cmpnd = Cmpnd ;#we don't have compound handling yet
LIST CLB = clb sent cm ;
LIST PUNCT = sent cm ;#TODO
LIST LEFT = LEFT ;#TODO
LIST RIGHT = RIGHT ;#TODO
LIST Guess = Guess ;#TODO



LIST TAG = n np vblex adj adv cnjcoo cnjsub ij pr prn p1 p2 p3 det pos nom acc gen
dat m f nt sg pl def ind comp sup pres past inf pp
ppres supn imp ; #TODO the rest


LIST NAGD = nom acc gen dat ;
LIST GENDER = m f nt ;
LIST NUMBER = sg pl ;

# Sets
# ====

# Noun sets


SET NounMscFem = (n m) OR (np m) OR (n f) OR (np f) ;
SET NounMscNeu = (n m) OR (np m) OR (n nt) OR (np nt) ;
SET NounFemNeu = (n f) OR (np f) OR (n nt) OR (np nt) ;

SET MscFem = Msc OR Fem ;
SET MscNeu = Msc OR Neu ;
SET FemNeu = Fem OR Neu ;

LIST 3PERS = "hann" "hon" "tad" ;

# Adjective sets

LIST REALADJ = adj ;#TODO: was A OR A*, what does that mean?


# Nominal sets

LIST NOMINALHEAD = n np num (prn p1) (prn p2) (prn p3) Refl Recipr (prn itg) (prn ind) ;#TODO: this had something like "Pron Interr" -- does that work??

LIST PRENAGR = det adj ;

LIST MIDJA = "miðja" ; # in the middle of - construction. More N in here, also bottle constr.

SET NOT-ACC = TAG - Acc ;


# Verb sets

LIST COPULA = "vera" "verða" ;

LIST AUX = "kunna" "skula" "vera" "verða" "hava" "munna" ;

LIST VFIN = pres past imp Sbj ;#TODO
LIST INDSBJ = pri Sbj ;#TODO
#LIST Ind = Prs Prt ;

LIST NONTHIRDV = p1 p2 ;

LIST VINFIN = inf supn pp pprs ;

LIST MODV = "kunna" "láta" "skula" "vilja" "munna" "mega" ;

LIST DATV = "bjóða" "hýsa" "sleppa" "smakka" "takka" ; # "undirvísa" 
 # sleppa3 = sleppa manninum leysum
 
LIST DATPREPV = "liggja" "vera" "standa" "fiska" "vera"; # use only ACCPREPV
LIST ACCPREPV = "leggja" "fara" "renda" "koma" "liða" "seta" ;

LIST OBJPREDVERB = "kalla" "doypa" "nevna" "taka"  ;

LIST MOVEMENTVERB = "koma" "koyra" ;

# Noun-Verb sets

LIST THIRDSG = sg (p3 sg) ;
LIST THIRDPL = sg (p3 pl) ;
LIST THIRD = sg p3 pl ;

# Number sets

SET NUMBERS = Num - ("eitt") - ("1") ;
# Preposition sets

LIST ACCPREP = "aftan" "aftanvert" "ábeint" "áraka" "foruttan" "gjøgnum" "hóast" "inntil" "ígjøgnum" "íkring" "kring" "niðan" "oman" "síðan" "umframt" "umhvørvis" "umkring" "báðumegin" "hasumegin" "hvørgumegin" "høgrumegin" "sínumegin" "skeivumegin" "somumegin" "vinstrumegin" "øðrumegin" "eystan" "norðan" "sunnan" "vestan" ;

LIST DATPREP = "hjá" "með" "aftrat" "afturat" "afturímóti" "andstøðis" "andsýnis" "frá" "mótvegis" "nær" "sambært" "viðvíkjandi" ;

LIST ACCDATPREP = "í" "á" "undir" "yvir" "eftir" "fyri" "við" ;

LIST ACCGENPREP = "innan" "uttan" "millum" "til" "vegna" ;

LIST ACCDATGENPREP = "at" "av" "hjá" "móti" "um" "undan" "úr" ;


SET SOMEACCPREP = ACCPREP | ACCDATPREP | ACCGENPREP | ACCDATGENPREP ;

SET SOMEDATPREP = DATPREP | ACCDATPREP | ACCDATGENPREP ;

SET SOMEGENPREP = ACCGENPREP | ACCDATGENPREP ;

SET SOMEACCDATPREP = ACCDATPREP | ACCDATGENPREP ;

# Boundary sets

SET S-BOUNDARY = CS | Interr | (";") | BOS | EOS ;
SET S-BOUNDARY2 = CS | Interr | (";") | BOS | EOS | (@CVP) ;

# Complementary set

# Case sets

SET NOTNOM = Dat | Gen | Acc ;
SET NOTDAT = Nom | Gen | Acc ;
SET NOTACC = Nom | Gen | Dat ;
SET NOTACCDAT = Nom | Gen ;
SET OBL = Acc | Dat | Gen ;

#SET NO

SET PRE-N =  A | Det | (n gen) | (np gen) | Num | (prn gen) | CC ; # Det???

SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

LIST COMMA = (",") cm ;
SET MARK =  COMMA | ("\") | ("\;") ; #"


SET WORDMARK = WORD | MARK ;

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;
SET NOT-A = WORDMARK - A ;
SET NOT-CC = WORDMARK - CC ;
SET NOUNADJ = N | A ;

SET NP-MEMBER = PRE-N | N | Pron ;

LIST TIME = "sunnudagur" ;



# Semantic sets


LIST ABSTRACT = "ráð" "byráð" ;





SECTION


REMOVE Guess ; # If any other reading is possible.


# Early and popular rules


SELECT:r50 Inf IF (-1 ("at"));
SELECT:r4 CS IF (0 ("at"))(1C Inf);
SELECT:r2 Pr IF (0 ("á"))(1 Dat OR Acc);

# at
REMOVE:r36 Pr IF (0 ("at"))(NOT *1 Acc OR Gen OR Dat BARRIER V);
SELECT:r37 CS IF (0 ("at"))(*1 Nom BARRIER NPNHA LINK *1 V BARRIER OBL);

REMOVE:rAtN N IF (0 ("at"))(NOT -1 PRE-N) ;
	## soleiðis at kvøða einsamallur.

# Adjective disambiguation
# ------------------------

REMOVE:r18 A + Fem IF (0  $$NAGD) (*1 NounMscNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Fem);
REMOVE:r19 A + Msc IF (0  $$NAGD) (*1 NounFemNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Msc);
REMOVE:r20 A + Neu IF (0  $$NAGD) (*1 NounMscFem + $$NAGD BARRIER NPNHA LINK NOT 0 Neu);

SELECT A IF (-3 COPULA)(-2C A)(-1 CC)(1 CLB);
SELECT A IF (-1 COPULA)(1 CC)(2C A)(1 CLB);



# Case disambiguation
SELECT:r21 $$NAGD IF (0C A)(*1C $$NAGD BARRIER NPNHA)(NOT 0 V); #XXX C?
SELECT:r21b $$NAGD IF (0C A)(-*1C $$NAGD BARRIER NPNHA)(NOT 0 V); #XXX C?
REMOVE:r22 $$NAGD IF (0 A)(*1C N BARRIER NPNHA LINK NOT 0 $$NAGD);

REMOVE:AVS OBL IF (*-1 VFIN BARRIER NPNH LINK *-1 BOS OR CLB BARRIER NOT-ADV)(0 (n nom) OR (np nom));
REMOVE:AVS2 OBL IF (-2 CLB)(-1 Adv OR CC OR CS)(0 Nom)(1 V + THIRDSG)(NOT *2 Nom BARRIER CLB);

REMOVE Nom IF (-2 V)(-1C Pr)(0 N)(1 CLB); # Too specific, this.

REMOVE Dat IF (*-1 V LINK NOT 0 DATV BARRIER NPNH)(0 Acc)(1 EOS OR CS);
	## at fáa høvi at...




# PP disambiguation
# =================

# Preposition or not?

#REMOVE Pr IF (NOT *-1 S-BOUNDARY LINK *1 OBL BARRIER NPNH)(0 Adv)(NOT *1 OBL BARRIER NPNH)
REMOVE Pr IF (0 Adv)(NOT *1 OBL BARRIER NPNH);
# This rule does not handle stranded prepositions, it must be relaxed.

REMOVE:PrAdv Adv IF (*-1 N OR V)(0 Pr + SOMEACCPREP)(*1 Acc BARRIER NPNH);
	## Altjóða samstarv innan gransking og útbúgving.

# á
REMOVE:AArule N IF (0 ("á" ind))(-1C NOMINALHEAD OR V);
REMOVE:AArule N IF (0 ("á" ind))(-1C NOMINALHEAD OR V);

# millum
REMOVE:r38 Adv IF (0 Dat OR Acc)(*-1 ("millum") BARRIER NPNH);


# til
SELECT:r39 Pr IF (0 ("til"))(*1 N OR NPNH OR ("at") BARRIER NPNHA);

SELECT:r3 Pr IF (0 ("til"))(*1 (n gen) BARRIER NPNHA);

# um
SELECT:um1 CS IF (-1 ("sjálvur"))(0 ("um"));
REMOVE:um2 CS IF (0 ("um"));

# Case within PP phrases

SELECT:r40 Acc IF (*-1 ACCPREP BARRIER NOTACC OR V OR Pr);
SELECT:r41 Dat IF (*-1 DATPREP BARRIER NOTDAT OR V OR Pr);

REMOVE:r42 NOTDAT IF (*-1 DATPREP BARRIER NOTDAT OR S-BOUNDARY OR V OR Pr);
#REMOVE Acc IF (*-1 DATPREP BARRIER NOTDAT);
#REMOVE Gen IF (*-1 DATPREP BARRIER NOTDAT);

REMOVE:r43 Gen IF (*-1 ACCDATPREP BARRIER NOTACCDAT OR S-BOUNDARY OR V OR Pr OR CLB);
REMOVE:r44 Nom IF (*-1 ACCDATPREP BARRIER NOTACCDAT OR S-BOUNDARY OR V OR Pr OR CLB);
# CLB makes it weaker, be prepared to compensate.

REMOVE:AccPronNotIndef (n ind) IF (-1 ACCPREP OR ACCDATPREP)(0 (prn acc)) ;
REMOVE:AccPronNotIndef (np ind) IF (-1 ACCPREP OR ACCDATPREP)(0 (prn acc)) ;
REMOVE:DatPronNotIndef (n ind) IF (-1 DATPREP OR ACCDATPREP)(0 (prn dat)) ;
REMOVE:DatPronNotIndef (np ind) IF (-1 DATPREP OR ACCDATPREP)(0 (prn dat)) ;

SELECT:AccNP (n acc) IF (-1C (det acc));
SELECT:AccNP (np acc) IF (-1C (det acc));

SELECT (adj dat) IF  (*-1 ACCDATPREP BARRIER NPNH)(1 (n dat) OR (np dat));

# REMOVE:Fragment Dat IF (*-1 ACCDATPREP BARRIER NPNH)(NOT *-1 V)(NOT *1 V);
# but there are such fragments!

REMOVE:ACCPREPVERB Dat IF (*-1 ACCDATPREP BARRIER NPNH LINK *-1 ACCPREPV BARRIER NOT-ADV)(0 Acc);
REMOVE:DATPREPVERB Acc IF (*-1 ACCDATPREP BARRIER NPNH LINK *-1 DATPREPV BARRIER NOT-ADV)(0 Dat);

REMOVE:STRONGDATPREPVERB Acc IF (*-1 SOMEACCDATPREP BARRIER NPNH LINK *-1 S-BOUNDARY BARRIER ACCPREPV)(0 Dat);
#REMOVE:STRONGDATPREPVERB Acc IF (*-1 ACCDATPREP BARRIER NPNH LINK *-1 Pr BARRIER ACCPREPV); # BARRIER NOT-ADV)(0 Dat);

# POS disambiguation
# ==================

# Pron Pers or Det

REMOVE Pers IF (-1 Pr)(0 (det gen))(*1 N OR A BARRIER NPNH);
       ## vendu móti hansara bygd.

# Det

REMOVE:r1 (num) IF (0 ("ein" det))(*1 N OR A BARRIER NPNHA);
	## eina mynd

# Prop

SELECT:InternalProp Prop IF (NOT -1 (clb OR sent OR cm OR LEFT))(NOT 0 Guess);#TODO LEFT=lpar??

# Specific lexemes, words 
# =======================

# at
SELECT CS IF (-2 ("av"))(-1 ("tað"))(0 ("at"));
REMOVE Pr IF (0 ("at"))(NEGATE 1 NP-MEMBER);

# á 
SELECT Interj IF (-1 BOS)(0 ("á"))(1 COMMA OR CC);
       ## Á, eg veit ikki.
# ár
SELECT (nt sg acc) IF (-1 ("i"))(0 ("ár"));

# bara
SELECT ("bara" adv) IF (1 Pron);
SELECT ("bara" adv) IF (*1 N OR A BARRIER NPNH);
       ## Bara eygað sá roykin fara uppeftir sum óljóð.

# eg
SELECT:eg ("eg" prn p1 pl nom) IF (*1 (vblex pl) BARRIER NOT-ADV); #(*1 V + Pl BARRIER NOT-ADV);
	  ## Vit mugu sleppa.
#TODO this probably doesn't work ("vit" ? prnpers?)
# ein
REMOVE ("eini") IF (0 Dat + Dat )(1 A + Dat OR N + Dat) ;


# eingin
SELECT:eingin ("eingin") IF (-1 ("til"))(0 Gen);
	      ## til einkis.

# hafa
SELECT ("hava") IF (0 ("høva"))(*1 Sup BARRIER NOT-ADV);
SELECT ("hava") IF (0 ("høva"))(-1 Sup);
       ## Teir høvdu lýtt á kongin.

# hann
REMOVE:hannur ("hannur") IF (NOT -1 PRE-N)(0 ("hann")); # hannur must be ein hannur or smth

# her
REMOVE ("hera" imp) IF (0 ("her"));
	# Cannot come up with a condition calling for Imp of "hera"

# hon
SELECT:hon Pron IF (0 ("hon"))(NOT 0 PRE-N);

# húsi
REMOVE:húsi ("húsi") IF (*-1 MOVEMENTVERB)(0 ("hús"));
	    ## koma til húsa.

# ið
SELECT CS IF (-1 CLB)(0 ("ið"));

# innan
REMOVE ("inni") IF (0 ("innan" pr))(*1 Acc BARRIER NPNH);
	## Samstarv innan gransking.
	
# liggja
SELECT ("liggja") + Prt IF (-1 N OR Pron) ;
SELECT ("liggja") + Prt IF (1 Pr);

# men
SELECT CC IF (-1 CLB OR BOS OR PUNCT)(0 ("men"));

# niðan
SELECT Adv IF (0 ("niðan"))(1 Pr);

# seg
SELECT Refl IF (0 ("seg" Refl $$NAGD))(1 ("sjálvur" + $$NAGD));#TODO Refl

# sjalvur
SELECT Adv IF (0 ("sjálvt"))(1 ("um") OR EOS);
       ## Tey vóru so hugnalig, sjálft um tey sóu hjálparleys út.

# skal
REMOVE ("skal") IF (0 ("skula"))(1 Inf);

# tann
SELECT ("tann" det) IF (1 ("sum"));
SELECT ("tann" det $$NAGD) IF (*1 N + $$NAGD OR A + $$NAGD BARRIER NPNH); 

# tá and tá ið
SELECT Adv IF  (0 ("tá"))(1 (("ið") OR V OR Det OR Pron OR N); #TODO: wtf? the parens don't match!!
SELECT Adv IF (-1 ("tá"))(0 ("ið"));
SELECT Adv IF (-1 Adv OR NPNH OR COMMA)(0 ("tá"))(1 Det OR Pron OR V);#TODO: had to remove the parens for this to work (since there's no ("foo") in there… silly visl)
	# Og tá eg havi...
	# Fá boð, tá nýtt er at frætta.
	
SELECT Adv IF (-1 NPNH)(1 Ind OR Sbj);

REMOVE:r5 Det IF (0 Pers)(NEGATE 0 Gen LINK -1 N)(NOT 1 NPNHA OR N);


# um
REMOVE Imp IF (0 ("um")) ;
# This is against the verb ymja, súsa, brúsa, dynja, (kvæð.) munnur og nasar umdu í blóði 
# (blóðið fossaði)
# In order to write a good rule for this we need examples of imperative use (which is "um").
# I did not intend to write a 'remove all ymja' rule, but simply cannot come up with good 
# contexts including 'ymja' but excluding 'um'.

# væl
REMOVE Imp IF (NOT -1 BOS)(0 ("væl"))(NOT 1 ("um"));
# Again, we need real-life examples for imperative of
# væla 2 -di s 1: ~ um 1 bøta um, hjálpa upp á, umvæla, ~ um húsini 
#     2 (sj.) hjúkla um, hugsa ikki um at ~ um meg

# á
REMOVE Interj IF (1 WORD)(0 ("á" pr));
REMOVE Interj IF (-1 V)(0 ("á" pr));

# General adverb
SELECT:r6 Adv IF (-1 DATPREP)(*1 Dat BARRIER NPNHA);

SELECT:r7 Adv IF (-1 VFIN)(1 Inf);

REMOVE Adv IF (-1 Det OR Pr)(0 A)(1 N);
       ## ið stakk í bleyta jørð,



# Idioms

SELECT ("stórur" adj nt sg dat) IF (-2 ("í"))(-1 ("tað"))(1 ("og"))(2 ("heilur"));
SELECT ("heilur" adj nt sg dat) IF (-4 ("í"))(-3 ("tað"))(-2 ("stórur"))(-1 ("og"));

SELECT ("væl" adv) IF (1 ("skera" pp));
SELECT ("skera" pp) IF (-1 ("væl" adv));
	## vel skorin

SELECT ("innast" adv) IF (1 ("inni"));
	## innast inni
	
# NP internal constraints
# =======================

# Determiner disambiguation

REMOVE:r8 Neu IF (0 Det + $$NAGD)(NOT 0 Poss + $$NAGD LINK -1 N + $$NAGD)(*1 NounMscFem + $$NAGD BARRIER NPNHA LINK NOT 0 Neu);
REMOVE:r9 Msc IF (0 Det + $$NAGD)(NOT 0 Poss + $$NAGD LINK -1 N + $$NAGD)(*1 NounFemNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Msc);
REMOVE:r10 Fem IF (0 Det + $$NAGD)(NOT 0 Poss + $$NAGD LINK -1 N + $$NAGD)(*1 NounMscNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Fem);


REMOVE:r11 Neu IF (0 A + $$NAGD)(*1 NounMscFem + $$NAGD BARRIER NPNHA LINK NOT 0 Neu);
REMOVE:r12 Msc IF (0 A + $$NAGD)(*1 NounFemNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Msc);
REMOVE:r13 Fem IF (0 A + $$NAGD)(*1 NounMscNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Fem);

REMOVE:Def Def IF (0 A + $$NAGD)(*1 A OR N BARRIER NPNHA LINK 0C $$NAGD + Indef);
REMOVE:Def Indef IF (0 A + $$NAGD)(*1 A OR N BARRIER NPNHA LINK 0C $$NAGD + Def);


# Postnominal determiner disambiguation

SELECT:PossNum Sg IF (0 Poss)(-1C N + Sg);
SELECT:PossNum Pl IF (0 Poss)(-1C N + Pl);

REMOVE:r8b Neu IF (0 Poss + $$NAGD)(-1 NounMscFem + $$NAGD LINK NOT 0 Neu);
REMOVE:r9b Msc IF (0 Poss + $$NAGD)(-1 NounFemNeu + $$NAGD LINK NOT 0 Msc);
REMOVE:r10b Fem IF (0 Poss + $$NAGD)(-1 NounMscNeu + $$NAGD LINK NOT 0 Fem);


#REMOVE:r11b Neu IF (0 A + $$NAGD)(-1 NounMscFem + $$NAGD LINK NOT 0 Neu);
#REMOVE:r12b Msc IF (0 A + $$NAGD)(-1 NounFemNeu + $$NAGD LINK NOT 0 Msc);
#REMOVE:r13b Fem IF (0 A + $$NAGD)(-1 NounMscNeu + $$NAGD LINK NOT 0 Fem);

# Definiteness disambiguation

REMOVE Def IF (0 A + Indef + $$NAGD)(1C N + Indef + $$NAGD);
REMOVE Indef IF (0 A + Def + $$NAGD)(1C N + Def + $$NAGD);


# Case disambiguation

SELECT:r14 $$NAGD IF (0 Det)(NOT 0 Poss LINK -1 N)(*1C $$NAGD BARRIER NPNHA);
REMOVE:r15 $$NAGD IF (0 Det)(NOT 0 Poss LINK -1 N)(*1 N  OR A  BARRIER NPNHA LINK NOT 0 $$NAGD);						  
SELECT:r16 $$NAGD IF (0 Det)(NOT 0 Poss LINK -1 N)(*1C $$NAGD BARRIER NPNHA);
REMOVE:r17 $$NAGD IF (0 Det)(NOT 0 Poss LINK -1 N)(*1 N OR A   BARRIER NPNHA LINK NOT 0 $$NAGD);						  

# Noun disambiguation
# -------------------

SELECT:r28 $$GENDER IF (-1C (adj) + $$GENDER)(0 N);
SELECT:r30b $$GENDER IF (-1C (det) + $$GENDER)(0 N);
SELECT:r30c $$GENDER IF (-1C (prn ind) + $$GENDER)(0 N);

SELECT:r29 $$NAGD IF (-1C (adj) + $$NAGD)(0 N);
SELECT:r30b $$NAGD IF (-1C (det) + $$NAGD)(0 N);
SELECT:r30c $$NAGD IF (-1C (prn ind) + $$NAGD)(0 N);

SELECT:r30 $$NUMBER IF (-1C (adj) + $$NUMBER)(0 N);
SELECT:r30b $$NUMBER IF (-1C (det) + $$NUMBER)(0 N);
SELECT:r30c $$NUMBER IF (-1C (prn ind) + $$NUMBER)(0 N);


# Poss disambiguation

REMOVE:r31 $$GENDER IF (-1 N LINK NOT 0 $$GENDER)(0 Poss);

REMOVE:r32 $$GENDER IF (0 Poss)(*1 N OR A BARRIER NPNHA LINK NOT 0 $$GENDER);

SELECT:DisPostPoss Poss IF (-1 N + $$GENDER + $$NAGD)(0 $$GENDER + $$NAGD); #(NOT 1 ...)

# Number disambiguation
REMOVE:r33 Pl IF (*-1 ("eitt" num sg) OR (det sg) BARRIER NOT-A)(0 (n sg) OR (np sg));
# Here, we need vislcg3 and variable notation!

SELECT:NumNPl (n pl) IF (-1 NUMBERS);
SELECT:NumNPl (np pl) IF (-1 NUMBERS);


# Coordination


REMOVE Def   IF (0 N + Indef)(1 CC)(2C N + Indef);
REMOVE Indef IF (0 N + Def)  (1 CC)(2C N + Def);


# NP head disambiguation
# ======================

# Inversion

REMOVE:r34 Acc IF (-2 CS)(-1 (vblex pl))(0 (pl nom)) ;
	## Tað var, sum váru tey sett út úr luftini.


# A or N
# ======
REMOVE:r35 A IF (*-1 Pr OR CLB BARRIER NPNH)(0 N)(1 S-BOUNDARY OR V);

REMOVE (adj f) IF (0 (n nt) OR (np nt))(-1 COPULA)(-2 ("tað" nom));
REMOVE (adj m) IF (0 (n nt) OR (np nt))(-1 COPULA)(-2 ("tað" nom));


# Elliptic AP as NP
# =================

SELECT:DefectAP Msc IF (0C REALADJ)(1 S-BOUNDARY) ;

# P chains or not

SELECT:r45 Adv IF (0 Pr)(1C Pr);
	## Hann fór niðan um Danmark.

# Pronoun disambiguation

REMOVE:r46 Det IF (0 Pers)(1C V);
	## Tey vóru so hugnalig.

REMOVE:r47 Det IF (0 Pers)(NEGATE *1 Def BARRIER NPNH)(NEGATE 0 Gen LINK -1 N);
	##

#NP Coordination

REMOVE:CCPrs Prs + 3Sg IF (0 Pl + Nom)(1 CC)(*2 Pl + Nom BARRIER NPNH OR OBL);



# VP disambiguation
# =================

# V or A

REMOVE:r48 A IF (0 ("vera"))(1 (n sg) OR (np sg));
	## Enskt mál er móðurmál.

SELECT:r49 ("vera" p3 sg) IF (-1 ("tað" prn p3 sg nom));#TODO does it work even if the <r> says prnpers<prn><p3> etc instead of tað<prn><p3> etc. ?
	## Tað var, sum váru tey sett av tilvild av einum fóti út úr luftini.

REMOVE:PrtNotAdj A IF (-1 NOMINALHEAD + Sg + Nom)(0 (vblex past sg) OR (vblex past p3 sg) OR (vblex past p1 sg)) ;

#REMOVE 
 
# Infinitive

SELECT:r51 Inf IF (*-1 MODV BARRIER V);

REMOVE (vblex pl) IF (-2 VFIN + Sg)(-1 (prn sg))(0 Inf);
REMOVE (vblex pl) IF (-1 N + Sg LINK *-1 VFIN + Sg BARRIER NPNH)(0 Inf);

# Imperative

REMOVE:ImpFirst N IF (-1 BOS OR PUNCT)(0 Imp)(*1 ("tín" pos) BARRIER VFIN);

REMOVE Imp IF (-1C Ind OR PrfPrc OR Inf OR PrsPrc OR Sup);
	## Gu∂ gjør∂i hvalvi∂.

REMOVE Imp IF (-1 CS)(0 Ind OR Sbj);

REMOVE Imp IF (-1 Adv OR Nom OR Pr OR CS OR V)(0 Prs OR Prt);
# This rule was commented out, but seems to be sensible, after all.

SELECT:CoordImp Imp IF (*-1 CC OR COMMA BARRIER VFIN LINK *-1C Imp BARRIER VFIN);
REMOVE:r52 Imp IF (NOT *-1 CC OR COMMA LINK *-1 Imp)(NOT *-1 BOS OR PUNCT BARRIER WORD); # hmm

REMOVE Sup IF (*-1 CLB BARRIER WORD)(0 Imp)(*1 CLB  BARRIER Ind);

# The Imp section must be written when there are genuine 
# examples to build upon.

REMOVE:r53 Imp IF (0 N)(1 CC)(*2 N BARRIER NPNHA);
	## Stýrið og stjórnin hjá...

REMOVE:r54 (imp pl) IF (0 (n nt) OR (np nt))(1 VFIN);
	## Húsið er stórt.

# Supine

SELECT:r55 Sup IF (*-1 ("fáa" vblex) BARRIER NOT-ADV);

REMOVE:r56 Sup IF (0 VFIN)(NOT *-1C VFIN BARRIER S-BOUNDARY)(NOT *1C VFIN BARRIER S-BOUNDARY);

REMOVE:SFregel N IF (0 Sup)(1 VFIN);

# Present singular

SELECT:r57 (vblex pri p3 sg) IF (-1 (np nom) OR (n sg nom) OR (prn p3 sg nom) LINK NOT *-1 CC BARRIER NPNHA)(1 (prn refl acc)) ;#TODO refl


REMOVE:test1 (vblex pri p1 sg) IF (NEGATE 0* ("eg" prn p1 sg nom)); 
REMOVE:test2 (vblex pri p2 sg) IF (NEGATE 0* ("tú" + prn + p2 + sg + nom)); # Tino! TODO what? +?

SELECT 1Sg IF (-1 ("eg" pron p1 sg nom));

REMOVE:r58b (vblex pri p1 sg) IF (*-1 (n sg nom) OR (np sg nom) BARRIER NOT-ADV LINK NOT *1 Pron + 1Sg BARRIER CLB) ;
SELECT:r59 (vblex pri p3 sg) IF (-1 (np nom) OR (n sg nom) LINK NOT *-1 CC BARRIER NPNHA OR S-BOUNDARY) ;

REMOVE 1Sg IF (-1 V)(0 Acc);
	"at fáa høvi at..."


REMOVE:CoordVerb (vblex pri p1 sg) IF (-1 CS OR CC LINK *-1C (vblex pri sg) OR (vblex pri p3 sg) BARRIER 1Sg);
		 ## Hann legði høkuna á knappin og læt eygað hvíla.


SELECT:r60 (vblex p2 sg) IF (*-1 ("tú" prn p2 sg nom) BARRIER NOT-ADV);

# Present plural

SELECT:r61 (vblex pl) IF (-1 (n pl nom) OR (np pl nom) OR ("eg" prn p1 pl nom));#TODO pl??? not "vit" then?


# V + Refl

SELECT:r62 ("seg" Refl) IF (-1 Inf OR 3Sg OR Pl) ;


# Nominative
# ==========

#REMOVE Acc IF (-1 CS OR BOS)(0 Nom)(1 VFIN OR Adv)(*2 S-BOUNDARY OR CLB OR ("sum") BARRIER NOMINALHEAD + Nom);
# hmm
# Fjallið lá stilt, tað ikki so mikið sum andi.

# Accusative
# ==========

REMOVE:r63 Nom IF (*-1 AUX OR MODV LINK -1 CC OR Nom BARRIER NPNH)(1 Inf);




# Genitive
# ========

# Genitive is marginal in Faroese. The strategy should be  
# 1. Write rules selecting genitive. (such rules are still forthcoming)

SELECT:TILRULE Gen IF (-1 ("til"))(0 (n ind) OR (np) OR Pron);


SELECT:r64 Gen IF (*-1 ACCGENPREP BARRIER NPNHA)(NOT 0 Acc);

# 2. Remove the remaining genitives. 

REMOVE:r65 Gen IF (NOT *-1 ACCGENPREP BARRIER NPNHA);


# Pronoun disambiguation
# =======================

REMOVE Det IF (0 Pron)(NOT 1 A OR N);

REMOVE ("hon") IF (*-1 (imp sg) BARRIER ("tú"));
   ## Tak við tær barnið og flýggja til Egyptalands.      

REMOVE:r66 ("hannur") IF (0 ("hann" nom))(1 (vblex p3 sg));
       ## Han var her.

SELECT (prn p1 pl nom) IF (1 ("sum"))(2 Pl);
SELECT (prn p2 pl nom) IF (1 ("sum"))(2 Pl);
SELECT (prn p3 pl nom) IF (1 ("sum"))(2 Pl);

REMOVE:r66b (pl nom ind) IF (1 (n sg) OR (np sg) OR (vblex sg) OR (v p3 sg));

REMOVE ("vit") IF (*-1 V + Pl BARRIER V)(0 (prn p1 pl nom));
	## Eru vit helst...

# Verb disambiguation
# ===================

SELECT:2Sg 2Sg IF (-1C ("tú" prn nom));
	   ## Tú ert.

SELECT:1Sg 1Sg IF (-1C Adv)(0C V)(1 ("eg" prn nom))(NOT 2 CC);
	## Fyrst vil eg, ...
	
REMOVE N IF (-1 ("sum" cnjsub))(0 INDSBJ)(NOT 1 INDSBJ);

# VP disambiguation
REMOVE:r113 V IF (-1 (det nom))(0 (n nom) OR (np nom));



# Number disambiguation

#SELECT $$NUMBER IF (0 A)(1 N + $$NUMBER);
REMOVE:r23 (adj sg) IF (1C N + Pl);
REMOVE:r23b (adj sg) IF (1 CC)(2 A + Pl)(3C N + Pl);
REMOVE:r24 (adj pl) IF (1C N + Sg);
	## í ta døkku moldina.


REMOVE (n pl) IF (*-1C (det sg) BARRIER NPNH) (0 (n sg) OR (np sg));
REMOVE (np pl) IF (*-1C (det sg) BARRIER NPNH) (0 (n sg) OR (np sg));

SELECT:r25 $$NUMBER IF (0 Det)(*1 N + $$NUMBER OR  A + $$NUMBER BARRIER NOT-A);

SELECT (num nom) IF (-1 BOS)(1 EOS);

# Gender disamb of numerals

SELECT:r26 $$GENDER IF (0 Num)(*1C N + $$GENDER BARRIER NOT-A);
	## eit sindur


# Case disamb of numerals

SELECT:r27 $$NAGD IF (0 Num)(*1C N + $$NAGD BARRIER NOT-A);


# Perhaps also rules that remove Neu if Msc or Fem, etc.


# Ordinals
REMOVE:r114 (adj sg) IF (1 (num sg));
	## fyrstu tríggjar mánðirnar.



# Coordination

SUBSTITUTE:einsog (cnjcoo) (cnjsub) TARGET ("og") IF (-1 ("ein" det nt sg gen));


SELECT Prt IF (NOT 0 OBL LINK *-1 Pr BARRIER NPNH)(-2C Prt)(-1 CC);
SELECT Prs IF (NOT 0 OBL LINK *-1 Pr BARRIER NPNH)(-2C Prs)(-1 CC);
SELECT Imp IF (NOT 0 OBL LINK *-1 Pr BARRIER NPNH)(-2C Imp)(-1 CC);

SELECT Prt IF (NOT 0 OBL LINK *-1 Pr BARRIER NPNH)(2C Prt)(1 CC);
SELECT Prs IF (NOT 0 OBL LINK *-1 Pr BARRIER NPNH)(2C Prs)(1 CC);
SELECT Imp IF (NOT 0 OBL LINK *-1 Pr BARRIER NPNH)(2C Imp)(1 CC);

SELECT A IF (0 (adj $$GENDER $$NAGD))(-1 CC)(-2 (adj $$GENDER $$NAGD));



#SELECT:AlwaysVfin VFIN IF (NOT 0* VFIN BARRIER CLB); # Tino!
  ## Hugur hansara føldist stórur og heitur , fylti alt upp inni í honum og bara hvíldi í sær sjálvum . 
  ## Læt ymist mala fram fyri eina løtu , til tað hevði tømt seg , fyri so at fara burtur av sær sjálvum 
# The first sentence indicates the 0* does not work.
# The second sentence indicates the rule is too strict.


# Substituting tags

SUBSTITUTE:sumPr (cnjsub) (pr) TARGET ("sum") (NOT -1 BOS OR COMMA);


# CC Coordinate NPs

SELECT (vblex pl) IF (-1 N + Nom + Sg LINK *-1 CC BARRIER NPNH LINK -1 N + Nom);






# Syntactic disambiguation
# ========================


# Syntax tags


LIST @←SUBJ = @←SUBJ ;
LIST @SUBJ→ = @SUBJ→ ;
LIST @←OBJ = @←OBJ ;
LIST @←IOBJ = @←IOBJ ;
LIST @OBJ→ = @OBJ→ ;
LIST @IOBJ→ = @IOBJ→ ;
LIST @→N = @→N ;
LIST @N← = @N← ;
LIST @→A = @→A ;
LIST @A← = @A← ;
LIST @IM = @IM ;
LIST @←SPRED = @←SPRED ;
LIST @SPRED→ = @SPRED→ ;
LIST @←OPRED = @←OPRED ;
LIST @OPRED→ = @OPRED→ ;
LIST @+FAUXV = @+FAUXV ;
LIST @-FAUXV = @-FAUXV ;
LIST @+FMAINV = @+FMAINV ;
LIST @-FMAINV = @-FMAINV ;
LIST @←ADVL = @←ADVL ;
LIST @ADVL→ = @ADVL→ ;
LIST @ADVL = @ADVL ;
LIST @P← = @P← ;
LIST @CNP = @CNP ;
LIST @CVP = @CVP ;
LIST @→CS = @→CS ;
LIST @APP = @APP ;
LIST @HNOUN = @HNOUN ;
LIST @Pron← = @Pron← ;
LIST @X = @X ;

LIST SYNTAG = (@CNP) (@CVP) (@←SUBJ) (@SUBJ→) (@←OBJ) (@OBJ→) (@←IOBJ) (@IOBJ→)  (@→A) (@A←) (@→N)  (@N←)  (@←SPRED) (@SPRED→) (@←OPRED) (@OPRED→) (@+FAUXV) (@+FMAINV) (@-FAUXV) (@-FMAINV) (@ADVL) (@←ADVL) (@ADVL→) (@P←) (@APP) (@HNOUN) ;



# ======== #
###SECTION  #
# ======== #


MAP (@IM) TARGET IM ;
MAP (@INTERJ) TARGET Interj ;  
ADD:r67 (@CNP) TARGET CC ;
ADD:r68 (@CVP) TARGET CS ;
MAP (@→CS) TARGET ("ein" det nt sg gen) IF (1 ("og" cnjsub));
MAP:sa (@→N) TARGET Prop + Poss IF (*1 N BARRIER NPNH);

MAP:ONEV (@+FMAINV) TARGET VFIN IF (NEGATE *0 V BARRIER S-BOUNDARY2);

MAP:r69 (@+FAUXV) TARGET AUX + VFIN IF (0*C VINFIN BARRIER CS OR CC);
# Added C. The rule introduces too many imperatives.
# An laternative restriction would be (0 NOT Imp)

MAP (@-FAUXV) TARGET VINFIN IF (0 AUX)(*-1 AUX BARRIER V) ;
MAP (@-FMAINV) TARGET VINFIN IF (*-1 AUX BARRIER V) ;

MAP:r70 (@+FAUXV) TARGET AUX + VFIN IF (*0 V BARRIER S-BOUNDARY2) ;
MAP:r70b (@+FMAINV) TARGET VFIN ;
#MAP (@+FMAINV) TARGET VFIN IF (NEGATE 0* VFIN BARRIER CS OR CLB);

MAP:DetPossGen (@N←) TARGET (det pos sg gen) IF (-1 N);
MAP:PostGenPron (@N←) TARGET (prn p1 gen) IF (-1C N + Indef)(NOT *1 N + Indef BARRIER NOT-A);
MAP:PostGenPron (@N←) TARGET (prn p2 gen) IF (-1C N + Indef)(NOT *1 N + Indef BARRIER NOT-A);
MAP:PostGenPron (@N←) TARGET (prn p3 gen) IF (-1C N + Indef)(NOT *1 N + Indef BARRIER NOT-A);
#MAP:PostGenPron (@N←) TARGET (prn p1 p2 p3 gen) IF (-1 N + Indef);
MAP:PreGenPron (@→N) TARGET (prn p1 gen) IF (*1 N + Indef BARRIER NOT-A) ;
MAP:PreGenPron (@→N) TARGET (prn p2 gen) IF (*1 N + Indef BARRIER NOT-A) ;
MAP:PreGenPron (@→N) TARGET (prn p3 gen) IF (*1 N + Indef BARRIER NOT-A) ;

ADD:r71 (@←SPRED) TARGET (n nom) OR (adj nom) IF (*-1 COPULA BARRIER NPNHA LINK 0 Imp);
ADD:r71 (@←SPRED) TARGET (np nom) OR (adj nom) IF (*-1 COPULA BARRIER NPNHA LINK 0 Imp);
ADD:r71 (@←SPRED) TARGET (n nom) OR (adj nom) IF (*-1 COPULA BARRIER NPNHA LINK -1 Nom);
ADD:r71 (@←SPRED) TARGET (np nom) OR (adj nom) IF (*-1 COPULA BARRIER NPNHA LINK -1 Nom);
#MAP (@SUBJ) TARGET (N Nom) OR (Num Nom) OR (Pron Nom);

MAP (@Pron←) TARGET ("sjálvur") IF (-1 Refl);
#MAP:PostPoss (@N←) TARGET (det pos $$NAGD $$GENDER) IF (-1 (n $$GENDER) OR (np $$GENDER))(NOT 1 (NPNH OR N $$GENDER $$NAGD));
MAP:PostPoss (@N←) TARGET Det + Poss + $$GENDER IF (-1 N + $$GENDER)(NEGATE 1 NOUNADJ + $$GENDER + $$NAGD);
#MAP PostPoss (@N←) TARGET (det pos $$GENDER) IF (-1 (n $$GENDER) OR (np $$GENDER));
# Number can be both NOMINALHEAD and Det, map that first

MAP (@→N) TARGET Num + $$NAGD IF (1 N + $$NAGD);
MAP (@→N) TARGET Num + $$NAGD IF (1 A + $$NAGD)(2 N + $$NAGD);

MAP:r72 (@SUBJ→) TARGET ("tú" prn p2 sg nom) IF (*1 (vblex p2 sg) BARRIER VFIN);
MAP:r73 (@←SUBJ) TARGET ("tú" prn p2 sg nom) IF (*-1 (vblex p2 sg) BARRIER VFIN);

MAP:r72b (@SUBJ→) TARGET ("eg" prn p1 sg nom) IF (*1 (vblex p1 sg) BARRIER VFIN);
MAP:r73b (@←SUBJ) TARGET ("eg" prn p1 sg nom) IF (*-1 (vblex p1 sg) BARRIER VFIN);

MAP:r75 (@←SUBJ) TARGET NOMINALHEAD + Sg + Nom IF (*-1 (vblex p3 sg) OR (vblex sg) BARRIER NPNH LINK NOT -1 Nom);
MAP:r74 (@SUBJ→) TARGET NOMINALHEAD + Sg + Nom IF (*1 (vblex p3 sg) OR (vblex sg));

MAP:mapcoordsubj2 (@SUBJ→) TARGET N + Nom IF (*-1 CC BARRIER NPNH LINK -1 N + Nom)(1 VFIN + Pl);
MAP:mapcoordsubj1 (@SUBJ→) TARGET N + Nom IF (1 CC LINK *1 N + Nom BARRIER NPNH LINK 1 VFIN + Pl);


MAP:r76 (@SUBJ→) TARGET NOMINALHEAD + Pl + Nom IF (*1 (vblex pl) BARRIER CLB);#(":" CLB) OR (";" CLB));
MAP:r77 (@←SUBJ) TARGET NOMINALHEAD + Pl + Nom IF (*-1 (vblex pl));

ADD:rOPR1 (@←OBJ) TARGET (n acc) OR (np acc) OR (prn acc) IF (*-1 OBJPREDVERB BARRIER Pr OR S-BOUNDARY)(*1 N + Acc BARRIER NPNHA);
MAP:rOPR2 (@←OPRED) TARGET (n acc) OR (np acc) OR (prn acc) OR (adj acc) IF (*-1 NOMINALHEAD + Acc BARRIER NPNHA LINK *-1 OBJPREDVERB BARRIER S-BOUNDARY);

MAP:r78 (@←OBJ) TARGET (n acc) OR (np acc) OR (num acc) OR (prn acc) IF (*-1 V BARRIER Pr OR S-BOUNDARY);
MAP:r78 (@←OBJ) TARGET (det acc) (*-1 V BARRIER Pr OR S-BOUNDARY)(NOT *1 N BARRIER NPNHA);
MAP:r79 (@←IOBJ) TARGET (n dat) OR (np dat) OR (num dat) OR (prn dat) IF (*1 (@←OBJ) BARRIER NPNHA);
MAP:r80 (@←OBJ) TARGET Dat IF (*-1 DATV);

# PP


MAP:r81 (@P←) TARGET NOMINALHEAD + Dat IF (*-1 SOMEDATPREP BARRIER NPNHA);
MAP:r82 (@P←) TARGET NOMINALHEAD + Acc IF (*-1 SOMEACCPREP BARRIER NPNHA);
MAP:r83 (@P←) TARGET NOMINALHEAD + Gen IF (*-1 SOMEGENPREP BARRIER NPNHA);

MAP:r81a (@P←) TARGET A + Dat IF (*-1 SOMEDATPREP BARRIER NPNHA)(NOT *1 N OR A);
MAP:r82a (@P←) TARGET A + Acc IF (*-1 SOMEACCPREP BARRIER NPNHA)(NOT *1 N OR A);
MAP:r83a (@P←) TARGET A + Gen IF (*-1 SOMEGENPREP BARRIER NPNHA)(NOT *1 N OR A);

MAP:r81 (@P←) TARGET NOMINALHEAD + Dat IF (*-1 CC BARRIER NPNHA LINK -1 NOMINALHEAD LINK *-1 SOMEDATPREP BARRIER NPNH);
MAP:r82 (@P←) TARGET NOMINALHEAD + Acc IF (*-1 CC BARRIER NPNHA LINK -1 NOMINALHEAD LINK *-1 SOMEACCPREP BARRIER NPNH);
MAP:r83 (@P←) TARGET NOMINALHEAD + Gen IF (*-1 CC BARRIER NPNHA LINK -1 NOMINALHEAD LINK *-1 SOMEGENPREP BARRIER NPNH);

#MAP:r84 (@→N) TARGET Det + $$GENDER + $$NAGD IF (1 N + $$GENDER + $$NAGD);
MAP:r84c (@→N) TARGET Det + $$GENDER + $$NAGD IF (1 A + $$GENDER + $$NAGD); # BARRIER NPNHA);
## sjekk denne!!

MAP:r84b (@→N) TARGET Pron + Indef  + $$GENDER + $$NAGD IF (1 N + $$GENDER + $$NAGD BARRIER NPNHA);

MAP:r84a (@→A) TARGET Det + $$GENDER + $$NAGD IF (1 A + $$GENDER + $$NAGD BARRIER NPNHA LINK 1 S-BOUNDARY);


MAP:r85 (@→N) TARGET (n gen) OR (np gen) IF (*1 N BARRIER V OR Adv);
MAP:r86 (@→N) TARGET Det IF (*1 N BARRIER V OR Adv OR (";"));
MAP:r87 (@→N) TARGET (det) IF (*1 N BARRIER V OR Adv);
MAP:r88 (@→N) TARGET A + $$NAGD OR Det + $$NAGD IF (*1 N + $$NAGD OR Num + $$NAGD BARRIER NPNHA);
MAP (@→N) TARGET Num IF (*1 N BARRIER NPNHA);

MAP:r89 (@VFIN) TARGET (vblex pres) OR (vblex past) OR (vblex imp);




MAP (@ADVL→) TARGET ("bara" adv) IF (1 Pron)(2 V);
MAP (@ADVL→) TARGET ("bara" adv) IF (*1 N BARRIER NPNH LINK 1 V);

# PPs

MAP:N+PP (@N←) TARGET Pr IF (-1 N)(*-2 BOS BARRIER V);

MAP:r90 (@←ADVL) TARGET Pr OR Adv IF (*-1 VFIN BARRIER CS);
MAP:r91 (@ADVL→) TARGET Pr OR Adv (NEGATE *-1 VFIN BARRIER CS)(*1 VFIN);

MAP:r92 (@N←) TARGET Poss IF (0 $$NAGD)(-1 N + $$NAGD);


MAP:advl (@ADVL) TARGET Adv OR Pr ;

MAP:r93 (@N←) TARGET N IF (0 $$NAGD)(-1 (n $$NAGD) OR (np $$NAGD));

MAP:r94 (@←SPRED) TARGET A IF (*-1 ("síggja") BARRIER NPNHA)(1 ("út"));
MAP:r95 (@←SPRED) TARGET A IF (-1 ("síggja"))(1 CC)(*2 A);

MAP (@P←) TARGET Det IF (-1 Pr)(1 EOS);
MAP:PostGen (@N←) TARGET (n gen) IF (-1 N);
MAP:PostGen (@N←) TARGET (np gen) IF (-1 N);
# MAP:PronPostGen (@N←) TARGET (prn gen) IF (-1 N); This is a Det


# Specific constructions
MAP (@N←) TARGET (n dat) IF (-1 MIDJA);
MAP (@N←) TARGET (np dat) IF (-1 MIDJA);

MAP (@→N) TARGET ("Harri") IF (1 ("Guð"));
    
# Fragments

MAP (@HNOUN) TARGET N IF (NOT *-1 VFIN)(NOT *1 Ind OR Sbj)(NOT 0 SYNTAG) ;
MAP (@HNOUN) TARGET A IF (NOT *-1 VFIN)(NOT *1 Ind OR Sbj)(NOT 0 SYNTAG) ;
MAP (@HNOUN) TARGET Num IF (-1 BOS)(1 EOS);

# We give in

MAP:r96 (@X) TARGET WORD ;




# =========== #
    ###SECTION   #
# =========== #


LIST (@SPRED) = @←SPRED @SPRED→ ;
LIST (@SUBJ) = @←SUBJ @SUBJ→ ;
LIST (@OBJ) = @←OBJ @OBJ→ ;
LIST (@IOBJ) = @←IOBJ @IOBJ→ ;



# No-syntax early
REMOVE:r97 (@X) ;

REMOVE:r98 (@SPRED) IF (NOT *0 (@SUBJ));#(NOT *1 (@SUBJ));
REMOVE:r99 (@OBJ) IF (*-1 Pr BARRIER NPNHA);
REMOVE:r100 (@IOBJ) IF (*-1 Pr BARRIER NPNHA);

REMOVE Acc IF (0 (sg @←SUBJ) LINK *-1 (vblex sg) OR (vblex p3 sg) BARRIER (@←SUBJ))(*-1 S-BOUNDARY BARRIER (@SUBJ→)); 

REMOVE:r101 (@P←) IF (*-1 BOS OR (n acc) OR (np acc) OR (n dat) OR (np dat) OR (n nom) OR (np nom) OR V OR CLB OR CS BARRIER Pr OR CC LINK NOT 0 Pr);

REMOVE:r102 (@→N) IF (0C N)(*-1 Pr BARRIER NPNHA);

SELECT:r103 (sg @←SPRED) IF (*-1 COPULA BARRIER NPNHA LINK -1 (sg nom));

SELECT:r104 (pl nom @←SPRED) IF (*-1 COPULA BARRIER NPNHA LINK -1 (pl nom));
SELECT:r121 (pl nom @←SPRED) IF (*-1 ("síggja") BARRIER NPNHA LINK -1 (pl nom))(*1 ("út") BARRIER NPNHA);

REMOVE:r105 (@←SUBJ) IF (*-1 VFIN BARRIER S-BOUNDARY LINK -1 (@SUBJ→));
REMOVE:r105 (@←SUBJ) IF (*-1 (@←SUBJ) BARRIER S-BOUNDARY LINK *-1 VFIN BARRIER NPNHA);

SELECT:r106 (@SUBJ→) IF (-1 CS)(1 VFIN);
	## sum hann hevði í lummanum.

SELECT (@→CS);

# NPs

REMOVE (@→N) IF (0 (gen @N←))(-1 N);

SELECT (n sg) IF (1C  Poss + Sg + @N←);
SELECT (np sg) IF (1C  Poss + Sg + @N←);
SELECT (n pl) IF (1C  Poss + Pl + @N←);
SELECT (np pl) IF (1C  Poss + Pl + @N←);

SELECT (pos $$NAGD) IF (-1C N + $$NAGD)(0 (@N←));

REMOVE (det m) IF (NOT 0 Poss)(*1C (n nt) BARRIER NPNH);
REMOVE (det m) IF (NOT 0 Poss)(*1C (np nt) BARRIER NPNH);
REMOVE (det f) IF (NOT 0 Poss)(*1C (n nt) BARRIER NPNH);
REMOVE (det f) IF (NOT 0 Poss)(*1C (np nt) BARRIER NPNH);
REMOVE (det nt) IF (NOT 0 Poss)(*1C (n m) BARRIER NPNH);
REMOVE (det nt) IF (NOT 0 Poss)(*1C (np m) BARRIER NPNH);
REMOVE (det f) IF (NOT 0 Poss)(*1C (n m) BARRIER NPNH);
REMOVE (det f) IF (NOT 0 Poss)(*1C (np m) BARRIER NPNH);
REMOVE (det m) IF (NOT 0 Poss)(*1C (n f) BARRIER NPNH);
REMOVE (det m) IF (NOT 0 Poss)(*1C (np f) BARRIER NPNH);
REMOVE (det nt) IF (NOT 0 Poss)(*1C (n f) BARRIER NPNH);
REMOVE (det nt) IF (NOT 0 Poss)(*1C (np f) BARRIER NPNH);

#REMOVE (@SUBJ→) IF (0 (@→N))(*1 (@SUBJ→) BARRIER NPNH);

REMOVE Imp IF (0 (@→N));
       ## Hitt størra ljós.

SELECT ($$NAGD) IF (0C (@→N))(*1C N + $$NAGD);

# Postverbal subjects

SELECT (@←SUBJ) IF (-2 (@OBJ→) OR (@ADVL→))(-1 VFIN + Pl)(0 Pl);
       ## Men her hafa tey ikki fullt yvirlit.


# Subjects

SELECT:onesubj (@SUBJ→) IF (1 VFIN)(NOT *2 (@←SUBJ) BARRIER CLB);

SELECT:defsubj (def) IF (NOT -1 Indef)(0C (@SUBJ→))(1 VFIN);
	       ## at talan var um ein dyst

SELECT:coordsubj (@SUBJ→) IF (*-1 CC BARRIER NPNH LINK -1 N)(1 VFIN + Pl);


# Third position subjects, postverbal subjects

SELECT:PostVSubj1 (@←SUBJ) IF (*-1 VFIN BARRIER NPNH LINK -1 BOS OR (@ADVL) OR (@P←) OR COMMA OR (PUNCT LEFT));#TODO LEFT
SELECT:PostVSubj2 (@←SUBJ) IF (*-1 VFIN BARRIER NPNH LINK -1 PUNCT LINK -1 COMMA); # (XX want more conditions)

# Topicalised non-subjects

# Indirect objects

REMOVE (@←IOBJ) IF (*-1 V BARRIER NPNH LINK NOT *-1 (@OBJ→))(NEGATE *1 (@OBJ→) BARRIER S-BOUNDARY OR CC OR V);

SELECT (@←OBJ) IF (*-1 CC BARRIER NPNH LINK -1C (@←OBJ));
REMOVE (@SUBJ→) IF (*-1 VFIN LINK -1C (@SUBJ→))(0 (@←OBJ));


# Direct objects

REMOVE Acc IF (*-1 ("seg" acc) LINK -1 V BARRIER NOT-ACC);
REMOVE:NotPostSubj (@SUBJ→) IF (0 (@←OBJ))(*1 CLB BARRIER VFIN);

REMOVE (@SUBJ→) IF (0 (@←OBJ))(NOT *1 VFIN BARRIER S-BOUNDARY);

# Subject predicatives

REMOVE (@←OBJ) IF (*-1 COPULA BARRIER NPNHA)(0 (@←SPRED));

# PPs

REMOVE:r107 (@←SUBJ) IF (*-1C Pr BARRIER NPNH)(1C (@P←));
REMOVE:r107a (@←SUBJ) IF (*-1C Pr BARRIER NPNH)(0 (@P←));

REMOVE:r107b (@SUBJ→) IF (*-1C Pr BARRIER NPNH)(1C (@P←));
REMOVE:r107c (@SUBJ→) IF (*-1C Pr BARRIER NPNH)(0 (@P←));

REMOVE:r108 (@←OBJ) IF (*-1C Pr BARRIER NPNH)(1C (@P←));
REMOVE:r108a (@←OBJ) IF (*-1C Pr BARRIER NPNH)(0 (@P←));

REMOVE:r109 (@←ADVL) IF (*-1C Pr BARRIER NPNH)(1C (@P←));

REMOVE:r110 (@←SPRED) IF (*-1C Pr BARRIER NPNH)(1C (@P←));
REMOVE:r110a (@←SPRED) IF (*-1C Pr BARRIER NPNH)(0 (@P←));

REMOVE:r111 (@←OPRED) IF (*-1C Pr BARRIER NPNH)(1C (@P←));
REMOVE:r111a (@←OPRED) IF (*-1C Pr BARRIER NPNH)(0 (@P←));

REMOVE:r112 V IF (-1 Pr)(0 (@P←));
REMOVE:r112b (@HNOUN) IF (*-1 Pr BARRIER NPNH)(0 (@P←));




	## av tilvild


# Number SPRED disambiguation

SELECT (nom @→N) IF (*1C N + Nom BARRIER NPNH OR PRE-N + NOTNOM);
SELECT (acc @→N) IF (1C N + Acc BARRIER NPNH OR PRE-N + NOTACC);
SELECT (dat @→N) IF (1C N + Dat BARRIER NPNH OR PRE-N + NOTDAT);


REMOVE:r115 (adj @→N) IF (0 V)(NOT 1 N OR A);
# Alternatively Demand Nom to the left or Num to the right.

SELECT Pl IF (-1 Pl + (@SUBJ→) LINK *-1 S-BOUNDARY BARRIER NOT-CC)(0 V);
       ## Men vit vita, at...


# OPRED

REMOVE (@SUBJ→) IF (0 (@←OPRED));
REMOVE (@←ADVL) IF (0 (@←OPRED));
# Postsyntactical verbal dis

#SELECT:r116 Ind IF (NEGATE 0* VFIN);

#SELECT Neu IF (0C (@←OPRED))(*-1 (@←OBJ) + Neu OR (@←OBJ) + ("tað"));
#so, does OPRED agree with OBJ or with SUBJ?

SELECT (p3 sg) IF (-1C (sg @SUBJ→));


SELECT:r117 CC IF (-1 BOS)(1C (@SUBJ));
	## Men tað var hált.
 	## Tað var, sum vóru tey sett.

REMOVE:TEST 1Sg IF (-1 (@SUBJ→))(0 3Sg);
REMOVE:TEST 1Sg IF (*1 (@←SUBJ) BARRIER NPNH)(0 3Sg);


# New go at NP disambiguation

SELECT $$GENDER IF (0 A)(-1 $$GENDER + (@→A));
SELECT $$NAGD IF (0 A)(-1 $$NAGD + (@→A));

REMOVE (@P←) IF (0 A + (@→N));

# REMOVE Acc IF 


# PP in NP

# Philosophy: PP in NP are static.
REMOVE Acc IF (0 Dat + (@P←))(*-1 Pr + (@N←) BARRIER NPNH);

# Specific lexemes

# Stray NPs

REMOVE:r118 (acc @HNOUN) IF (0 (nom @HNOUN));
REMOVE:r119 (gen @HNOUN) IF (0 (nom @HNOUN));
REMOVE:r120 (dat @HNOUN) IF (0 (nom @HNOUN));

REMOVE:NoHnoun (@HNOUN) IF (0* VFIN);


# Substitute rules

SUBSTITUTE:SUBS_vfin (@+FAUXV) (@rc-VAUX) TARGET V (*-1 ("sum" cnjsub) BARRIER VFIN OR CLB); 


SUBSTITUTE:AorB (@→N) (@P←) TARGET Det (-1 Pr LINK *-1 Pr)(1 CLB);
# Perhaps also demand an intermediate sem or og


SECTION

# Late rules

SELECT $$NAGD IF (0C Poss)(-1C N + $$NAGD);
